theory Protocol3
begin

/* ----------------- */
/* Equational theory */
/* ----------------- */

/* Load the built-in encryption theories */
//Do not change the following two lines, i.e. do not add, change or remove anything (not even comments) in the following two lines.
functions: f/2
builtins: asymmetric-encryption, symmetric-encryption, hashing 



/* -------------- */
/* Public Key rules */
/* -------------- */

/* The following rule should be annotated by the following action facts: 
- PubKey($A, ~ska)
*/
rule PubKey:  
[ Fr(~skX) ]
--[ PubKey($X, ~skX) ]->
  [ !Ltk($X, ~skX),
    !Pk($X, pk(~skX)),
    Out(pk(~skX)) ]

/* The following rule should be annotated by the following action facts: 
- RevealKey($A, ~skA)
*/
//un-comment the following line for Task 1.6
//rule RevealKey:
rule RevealKey:
  [ !Ltk($A, skA) ]
--[ RevealKey($A, skA), Reveal($A) ]->
  [ Out(skA) ]


/* -------------- */
/* Protocol rules */
/* -------------- */

/* The following rule should be annotated by the following action facts: 
- AliceSendsKey($A, $B, ~skA, pkB, ~Na)
*/
rule AliceSendsKey:
  [ Fr(~Na), 
    !Ltk($A, ~skA), // Lookup own sk
    !Pk($B, pkB) ] // Lookup B's pk
--[ AliceSendsKey($A, $B, ~skA, pkB, ~Na) ]->
  [ Out(<$A, aenc(~Na, pkB)>),
    AliceSendsKey($A, $B, ~skA, pkB, ~Na) ]

/* The following rule should be annotated by the following action facts: 
- BobReceivesKey($B, $A, ~skB, pkA, ~Nb, Na)
*/
rule BobReceivesKey:
  [ Fr(~Nb),
    !Ltk($B, ~skB),
    !Pk($A, pkA),
    In(aenc(Na, pk(~skB))) ]
--[ BobReceivesKey($B, $A, ~skB, pkA, ~Nb, Na) ]->
  [ BobReceivesKey($B, $A, ~skB, pkA, ~Nb, Na),
    Out(aenc(<~Nb, f(Na, ~Nb)>, pkA)),
    !Kab($A, $B, f(Na, ~Nb)) ]

/* The following rule should be annotated by the following action facts: 
- AliceReceivesNb($A, $B, ~skA, ~Na, Nb, ~ma, Kab)
*/
rule AliceReceivesNb:
  [ In(aenc(<Nb, Kab>, pk(~skA))), 
    AliceSendsKey($A, $B, ~skA, pkB, ~Na),
    Fr(~ma) ]
--[ AliceReceivesNb($A, $B, ~skA, ~Na, Nb, ~ma, Kab),
    Eq(Kab, f(~Na, Nb)),
    RunningA($A, $B, ~ma) ]->
  [ AliceReceivesNb($A, $B, ~skA, ~Na, Nb, ~ma, Kab),
    Out(senc(<~ma, h(Nb)>, Kab)) ]

/* The following rule should be annotated by the following action facts: 
- BobReceivesMa($B, $A, ~skB, ~Nb, Na, Kab, ~mb, ma)
*/
rule BobReceivesMa:
  [ In(senc(<ma, Nb_h>, Kab)),
    BobReceivesKey($B, $A, ~skB, pkA, ~Nb, Na),
    !Kab($A, $B, Kab),
    Fr(~mb) ]
--[ BobReceivesMa($B, $A, ~skB, ~Nb, Na, Kab, ~mb, ma),
    Eq(Nb_h, h(~Nb)),
    FinishedB($B, $A, ma, ~mb),
    SecretB(~mb),
    CommitB($B, $A, ma),
    RunningB($B, $A, ~mb),
    Honest($A), Honest($B) ]->
  [ Out(senc(<~mb, h(Na)>, Kab)),
    BobReceivesMa($B, $A, ~skB, ~Nb, Na, Kab, ~mb, ma) ]

/* The following rule should be annotated by the following action facts: 
- AliceReceivesMb($A, $B, ~skA, ~Na, ~ma, Kab)
*/
rule AliceReceivesMb:
  [ AliceReceivesNb($A, $B, ~skA, ~Na, Nb, ~ma, Kab),
    In(senc(<mb, Na_h>, Kab)) ] 
--[ AliceReceivesMb($A, $B, ~skA, ~Na, ~ma, Kab),
    Eq(Na_h, h(~Na)),
    SecretA(~ma),
    FinishedA($A, $B, ~ma, mb),
    CommitA($A, $B, mb),
    Honest($B), Honest($A) ]->
  [ ]



/* Restrictions*/
//You can un-comment these lines if you want to use this restriction to check equality
restriction Equality:
 "All x y #i. Eq(x,y) @i ==> x = y"


/* ---------- */
/* Properties */
/* ---------- */


/* Executability check: */
//Make sure to add the action facts referred to by this lemma in your model
//Do not change this lemma, i.e. do not add, change or remove anything (not even comments) in the following three lines.
lemma executable:
exists-trace 
"Ex #i #j A B ma mb. FinishedA(A, B, ma, mb)@i & FinishedB(B,A,ma,mb)@j& not (A=B)"

//un-comment following line for Task 1.5
lemma secrecyA:
all-traces
"All m #i. SecretA(m)@i ==> (not (Ex #j. K(m)@j)) | (Ex C #r. Reveal(C)@r & Honest(C)@i)"
/*FILL IN HERE: 
You may only use the action facts SecretA, SecretB, K, Honest,
and Reveal to formulate this lemma.*/

// un-comment following line for Task 1.5
lemma secrecyB:
all-traces
"All m #i. SecretB(m)@i ==> (not (Ex #j. K(m)@j)) | (Ex C #r. Reveal(C)@r & Honest(C)@i)"
/*FILL IN HERE: 
You may only use the action facts SecretA, SecretB, K, Honest,
and Reveal to formulate this lemma.*/

//un-comment following line for Task 1.5
lemma non_injectiveA:
all-traces
"All A B m #i. CommitA(A, B, m)@i ==> (Ex #j. RunningB(B, A, m)@j) | (Ex C #r. Reveal(C)@r & Honest(C)@i)"
/*FILL IN HERE: 
You may only use the action facts CommitA, CommitB, RunningA, RunningB, Honest,
and Reveal to formulate this lemma.*/

//un-comment following line for Task 1.5
lemma non_injectiveB:
all-traces
"All A B m #i. CommitB(B, A, m)@i ==> (Ex #j. RunningA(A, B, m)@j) | (Ex C #r. Reveal(C)@r & Honest(C)@i)"
/*FILL IN HERE: 
You may only use the action facts CommitA, CommitB, RunningA, RunningB, Honest,
and Reveal to formulate this lemma.*/


end
